buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'com.bmuschko:gradle-docker-plugin:3.2.0'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

jar {
  baseName = 'service'
  version = ''
}

group = 'com.circulosiete.curso.microservices'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


ext {
	springBootAdminVersion = '1.5.7'
	springCloudVersion = 'Edgware.SR2'
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('de.codecentric:spring-boot-admin-starter-client')
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	
	compile('org.springframework.cloud:spring-cloud-starter-zuul')
	testCompile('org.springframework.boot:spring-boot-starter-test')


	compile 'org.springframework.boot:spring-boot-starter-security'

  compile 'io.opentracing.contrib:opentracing-spring-web-autoconfigure:0.0.4'
  compile 'com.uber.jaeger:jaeger-core:0.18.0'
  compile 'io.opentracing.brave:brave-opentracing:0.20.0'
  compile 'io.zipkin.reporter:zipkin-sender-okhttp3:0.10.0'

}

dependencyManagement {
	imports {
		mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/Dockerfile')
	from 'openjdk:8u151-jre-slim'
  maintainer 'Aqui tu nombre "tu@tuempresa.com"'
  copyFile 'service.jar', '/opt/service.jar'
  exposePort 8765
  entryPoint 'java', "-Djava.awt.headless=true", "-Xms256m", "-Xmx256m", '-jar', '/opt/service.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.parentFile
  tag = 'circulo7/curso-ms-2018-febrero-gateway:' + project.version.toLowerCase()
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImage
  imageName = buildImage.tag
}

createDockerfile.dependsOn bootRepackage